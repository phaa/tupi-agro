#include "dht.h"
#include <WiFi.h>
#include <PubSubClient.h>
#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <ArduinoJson.h>

// Debug
#define DEBUG
#ifdef DEBUG
#define DEBUG_PRINT(x)  Serial.print (x)
#define DEBUG_PRINTLN(x)  Serial.println (x)
#else
#define DEBUG_PRINT(x)
#define DEBUG_PRINTLN(x)
#endif

// Config Sensor solo
#define SOIL_SENSOR_PIN 34
#define SOIL_MOISTURE_THRESHOLD 50 // Nível mínimo aceitável de umidade 30% 
#define MINIMUM_OPTIMAL_MOISTURE 70

// Configs DHT11
# define DHT11_PIN 21
dht DHT; 

#define SSID "Tupi"
#define PASSWORD "12345678"
WiFiClient espClient;

// Pensar no estado dela em caso de falta de energia
// Relés
# define PUMP_RELAY_PIN 4

// MQTT
PubSubClient MQTT(espClient);
StaticJsonDocument<66> doc;
StaticJsonDocument<66> doc2;
char buffer[130];
#define BROKER_MQTT "broker.hivemq.com"
#define BROKER_PORT 1883 
#define TOPICO_ESTUFA "tupi/agro/estufa" 
#define TOPICO_BOMBA "tupi/agro/bomba"
#define TOPICO_FERTIRRIGACAO "tupi/agro/fertirrigacao" 
#define ID_MQTT "TupiAgro_01" //id mqtt (para identificação de sessão)

//LCD
LiquidCrystal_I2C lcd(0x27, 16, 2);
short lcdState = 0;

// Variáveis de controle
byte pumpState = LOW;
byte exaustingState = LOW;
bool automaticIrrigation = true;
bool realTimeAnalysis = true;
bool mqttStatus = false;
short airHumidity = 0;
short airTemperature = 0;
short soilMoisture = 0;

// Auxiliar para o contador
unsigned long previousTime2s = 0;
unsigned long previousTime30min = 0;

void setup() {
  Serial.begin(115200);

  // Config LCD
  lcd.init();
  lcd.setBacklight(HIGH);
  lcd.setCursor(0, 0);
  lcd.print(F("Tupi Agro"));
  lcd.setCursor(0, 1);
  lcd.print(F("Inicializando..."));

  // Pinos adc
  pinMode(SOIL_SENSOR_PIN, INPUT);
  //analogReadResolution(11); // Default of 12 is not very linear. Recommended to use 10 or 11 depending on needed resolution.
  //analogSetAttenuation(ADC_6db); // Default is 11db which is very noisy. Recommended to use 2.5 or 6.

  // Pino da bomba
  pinMode(PUMP_RELAY_PIN, OUTPUT);
  digitalWrite(PUMP_RELAY_PIN, HIGH);
  
  // Métodos auxiliares
  initWifi();
  initMQTT();
  connectMQTT();

  lcd.setCursor(0, 1);
  lcd.print(F("Sistema Online"));
  delay(100);
}

void loop() {
  // Antes de fazer qualquer coisa verifica o status da conexão
  checkCommunication();

  if ( millis() - previousTime2s >= 2000 ) {
    if (realTimeAnalysis) {
      // Monitoramento em tempo real ligado
      DEBUG_PRINTLN("Hora de publicar 5s/true");
      checkGreenhouse(true, false);
    } 
    else {
      // Monitoramento desligado
      DEBUG_PRINTLN("Hora de publicar 5s/false");
      checkGreenhouse(false, false);
    }
    refreshLCD();
    previousTime2s = millis();
  }

  // A cada hora lê a estufa e manda os dados ao servidor
  if ( millis() - previousTime30min >= 3600000 ) {
    DEBUG_PRINTLN("Hora de publicar 1Hr/true");
    // Envia os dados ao servidor e armazena
    checkGreenhouse(true, true);
    previousTime30min = millis();
  }

  // Keep-alive da comunicação com broker MQTT
  MQTT.loop(); // Callback será executada automaticamente no decorrer do loop
}

//arquivo secundário

/*
 * Atributos do Wifi
 */
void initWifi() {
  WiFi.begin(SSID, PASSWORD);
  
  DEBUG_PRINT("(Wifi) Conectando à rede: ");
  DEBUG_PRINT(SSID);
  
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    DEBUG_PRINT(".");
  }
  
  DEBUG_PRINTLN("");
  DEBUG_PRINTLN("(Wifi) Conectado.");
  DEBUG_PRINT("(Wifi) IP address: ");
  DEBUG_PRINTLN(WiFi.localIP());
}

/*
 * Atributos do MQTT
 */
void initMQTT() {
  MQTT.setServer(BROKER_MQTT, BROKER_PORT); //informa qual broker e porta deve ser conectado
  MQTT.setCallback(callbackMQTT); //atribui função de callback (função chamada quando qualquer informação de um dos tópicos subescritos chega)
}

void connectMQTT() {
  if (WiFi.status() == WL_CONNECTED) {
    DEBUG_PRINTLN("(MQTT) Link ON");
    while (!MQTT.connected()) {
      if (MQTT.connect(ID_MQTT)) {
        mqttStatus = true;
        MQTT.subscribe(TOPICO_BOMBA);
        MQTT.subscribe(TOPICO_ESTUFA);
        DEBUG_PRINTLN("(MQTT) Conexão OK.");
      } 
      else {
        delay(1000);
      }
    }
  } 
  else {
    DEBUG_PRINTLN("(MQTT) Link OFF, reconectando.");
    WiFi.reconnect();
    connectMQTT();
  }
}

void callbackMQTT(char * topic, byte * payload, unsigned int length) {
  String msg;
  //obtem a string do payload recebido
  for (int i = 0; i < length; i++) {
    char c = (char) payload[i];
    msg += c;
  }

  String topicString = String(topic);

  if (topicString.equals(TOPICO_BOMBA)) {
    if (msg.equals("AI_ON")) {
      automaticIrrigation = true;
      MQTT.publish(TOPICO_BOMBA, "AI_ON_OK");
    }
    if (msg.equals("AI_OFF")) {
      automaticIrrigation = false;
      MQTT.publish(TOPICO_BOMBA, "AI_OFF_OK");
    }
    if (msg.equals("PUMP_ON")) {
      setPumpState(HIGH);
    }
    if (msg.equals("PUMP_OFF")) {
      setPumpState(LOW);
    }
  } 
  
  if (topicString.equals(TOPICO_ESTUFA)) {
    if (msg.equals("ANALYSIS_ON")) {
      realTimeAnalysis = true;
    }
    if (msg.equals("ANALYSIS_OFF")) {
      realTimeAnalysis = false;
    }
    if (msg.equals("GET_ARDUINO_STATE")) {
      doc2["pump"] = pumpState;
      doc2["exaust"] = exaustingState;
      doc2["ia"] = automaticIrrigation;
      size_t n = serializeJson(doc2, buffer);
      MQTT.publish(TOPICO_ESTUFA, buffer, n);
    }
  }
}

void checkCommunication(void) {
  if (!MQTT.connected()) {
    mqttStatus = false;
    connectMQTT(); //se não há conexão com o Broker, a conexão é refeita
  }
}

/*
 * Atributos LCD
 */
void refreshLCD() {
  lcd.clear();
  String displayMsg = "";
  switch (lcdState) {
    case 0:
      //coluna, linha
      lcd.setCursor(0, 0);
      displayMsg = "Ar T:" + String(airTemperature) + "C";
      lcd.print(displayMsg);
    
      lcd.setCursor(9, 0);
      displayMsg = "U:" + String(airHumidity) + "%";
      lcd.print(displayMsg);
    
      lcd.setCursor(0, 1);
      displayMsg = "Solo U:" + String(soilMoisture) + "%";
      lcd.print(displayMsg);
    
      lcd.setCursor(10, 1);
      displayMsg = "U:" + String((analogRead(SOIL_SENSOR_PIN)/4095)*3.3) + "%";
      lcd.print(displayMsg);
      break;
    case 1:
      lcd.setCursor(0, 0);
      displayMsg = (pumpState) ? F("Irrigacao: ON") : F("Irrigacao: OFF");
      lcd.print(displayMsg);
      lcd.setCursor(0, 1);
      displayMsg = (exaustingState) ? F("Exaustao: ON") : F("Exaustao: OFF");
      lcd.print(displayMsg);
      break;
    case 2:
      lcd.setCursor(0, 0);
      displayMsg = (mqttStatus) ? F("Sistema: ONLINE") : F("Sistema: OFFLINE");
      lcd.print(displayMsg);
      lcd.setCursor(0, 1);
      displayMsg = (automaticIrrigation) ? F("IA: ON") : F("IA: OFF");
      lcd.print(displayMsg);
      break;
  }

  //lcdState = (lcdState == 2) ? 0 : lcdState+1;
}

// Em breve colocar identificador da bomba
void setPumpState(byte state) {
  if (state) {
    // Relés ligados
    digitalWrite(PUMP_RELAY_PIN, LOW);
    pumpState = HIGH;
    MQTT.publish(TOPICO_BOMBA, "PUMP_ON_OK");
  } 
  else {
    // Relés desligados
    digitalWrite(PUMP_RELAY_PIN, HIGH);
    pumpState = LOW;
    MQTT.publish(TOPICO_BOMBA, "PUMP_OFF_OK");
  }
}

void checkGreenhouse(bool sendToServer, bool store) {
  soilMoisture = map(analogRead(SOIL_SENSOR_PIN), 1456, 2249, 100, 0);
  DHT.read11(DHT11_PIN);
  airHumidity = round(DHT.humidity);
  airTemperature = round(DHT.temperature);
  //a proxima leitura vai ser a média da anterior com essa
  // novo Agua: 305 Ar: 682
  // antigo 269, 632

  //Serial.println(analogRead(SOIL_SENSOR_PIN));
  DEBUG_PRINTLN(analogRead(SOIL_SENSOR_PIN));

  // Normaliza a entrada do sensor
  if (soilMoisture > 100) {
    soilMoisture = 100;
  } 
  else if (soilMoisture < 0) {
    soilMoisture = 0;
  }

  // Só analiza os sensores caso a irrigação automática esteja ligada
  if (automaticIrrigation == true) {
    if (soilMoisture < SOIL_MOISTURE_THRESHOLD) {
      setPumpState(HIGH);
    } 
    else if (soilMoisture >= MINIMUM_OPTIMAL_MOISTURE || pumpState == LOW) {
      setPumpState(LOW);
    }
  }

  if (sendToServer) {
    // prepara o pacote JSON para o servidor
    doc["soilMoisture"] = soilMoisture;
    doc["airTemp"] = airTemperature;
    doc["airHumidity"] = airHumidity;
    doc["store"] = (store) ? true : false;
    size_t n = serializeJson(doc, buffer);
    MQTT.publish(TOPICO_ESTUFA, buffer, n);
  }
}